
Multichar_Symbols

! Nominal tags:
%<n%>
%<sg%>
%<pl%>
%<indef%>
%<def%>
%<adj%>
%<utr%>
%<neu%>
%<prop%>

! Pronominal additions:
%<pron%>
%<subj%>
%<obj%>
%<px%> ! Possessive forms like hans, hennes
%<refl%>

! Verb tags:
%<v%>
%<inf%>
%<pres%>
%<pret%>
%<perf%>

! Tags for other POSes
%<adv%>
%<conj%>
%<det%>
%<prep%>
%<num%>

A1 ! Archiphoneme for a:0 alternations
E1 ! Archiphoneme for e:0 alternations
D1 ! Archiphoneme for d:t alternations

%  

LEXICON Root

Nouns ;
Adjectives ;
Adverbs ;
Conjunctions ;
Determiners ;
Prepositions ;
Pronouns ;
Propernouns ;
Verbs ;
Numerals ;

LEXICON N1 ! Utrum
%<n%>%<utr%>%<sg%>%<indef%>: # ;
%<n%>%<utr%>%<sg%>%<def%>:en # ;
%<n%>%<utr%>%<pl%>%<indef%>:ar # ;
%<n%>%<utr%>%<pl%>%<def%>:arna # ;

LEXICON N2 ! Neutrum
%<n%>%<neu%>%<sg%>%<indef%>: # ;
%<n%>%<neu%>%<sg%>%<def%>:et # ;
%<n%>%<neu%>%<pl%>%<indef%>: # ;
%<n%>%<neu%>%<pl%>%<def%>:en # ;

LEXICON N3 ! Utrum on -a
%<n%>%<utr%>%<sg%>%<indef%>:a  # ;
%<n%>%<utr%>%<sg%>%<def%>:an   # ;
%<n%>%<utr%>%<pl%>%<indef%>:or # ;
%<n%>%<utr%>%<pl%>%<def%>:orna # ;

LEXICON Adj-lex
%<adj%>%<sg%>%<indef%>%<utr%>:  # ;
%<adj%>%<sg%>%<indef%>%<neu%>:t # ;
%<adj%>%<pl%>:a  # ;
%<adj%>%<def%>:a # ;

LEXICON Adv-lex
%<adv%>: # ;

LEXICON Conj-lex
%<conj%>: # ;

LEXICON Det-en-lex
%<det%>%<indef%>%<utr%>:n # ;
%<det%>%<indef%>%<neu%>:tt # ;

LEXICON Det-den-lex
%<det%>%<def%>%<utr%>:n # ;
%<det%>%<def%>%<neu%>:t # ;

LEXICON Prep-lex
%<prep%>: # ;

LEXICON Proper-lex
%<n%>%<prop%>: # ;

LEXICON PRON_INGEN
 %<sg%>%<utr%>:en # ;
 %<sg%>%<neu%>:et # ;
 %<pl%>:a # ;

LEXICON Num-lex
%<num%>: # ;

LEXICON V-vara-lex
%<v%>%<inf%>:vara  # ;
%<v%>%<pres%>:är  # ;
%<v%>%<pret%>:var # ;
%<v%>%<perf%>:varit # ;

LEXICON V-er-lex
%<v%>%<inf%>:a   # ;
%<v%>%<pres%>:er # ;
%<v%>%<pret%>:te # ;
%<v%>%<perf%>:t  # ;

LEXICON V-ar-lex
%<v%>%<inf%>:a    # ;
%<v%>%<pres%>:ar  # ;
%<v%>%<pret%>:ade # ;
%<v%>%<perf%>:at  # ;

LEXICON V-veta-lex
%<v%>%<inf%>:eta    # ;
%<v%>%<pres%>:et    # ;
%<v%>%<pret%>:isste # ;
%<v%>%<perf%>:isst  # ;

LEXICON V-kunna-lex
%<v%>%<inf%>:unna   # ;
%<v%>%<pres%>:an    # ;
%<v%>%<pret%>:unde  # ;
%<v%>%<perf%>:unnat # ;

LEXICON V-ha-lex
%<v%>%<inf%>:    # ;
%<v%>%<pres%>:r  # ;
%<v%>%<pret%>:de # ;
%<v%>%<perf%>:ft # ;

LEXICON V-se-lex
%<v%>%<inf%>:e    # ;
%<v%>%<pres%>:er  # ;
%<v%>%<pret%>:åg  # ;
%<v%>%<perf%>:ett # ;

LEXICON V-gå-lex
%<v%>%<inf%>:å    # ;
%<v%>%<pres%>:år  # ;
%<v%>%<pret%>:ick # ;
%<v%>%<perf%>:ått # ;

LEXICON V-ta-lex
%<v%>%<inf%>:a     # ;
%<v%>%<pres%>:ar   # ;
%<v%>%<pret%>:og   # ;
%<v%>%<perf%>:agit # ;

LEXICON V-äta-lex
%<v%>%<inf%>:äta   # ;
%<v%>%<pres%>:äter # ;
%<v%>%<pret%>:åt   # ;
%<v%>%<perf%>:ätit # ;

LEXICON V-dricka-lex
%<v%>%<inf%>:icka   # ;
%<v%>%<pres%>:icker # ;
%<v%>%<pret%>:ack   # ;
%<v%>%<perf%>:uckit # ;

LEXICON Nouns

hund             N1 "dog"       ;
hus              N2 "house"     ;
träd             N2 "tree"      ;
svar             N2 "answer"    ;
år               N2 "year"      ;
trädgård         N1 "garden"    ;
mamma:mamm       N3 "mother"    ;
katt             N1 "cat"       ;
väg              N1 "way, road" ;
flicka:flick     N3 "girl"      ;
dag              N1 "day"       ;
väder:vädE1r     N2 "weather"   ;
fönster:fönstE1r N2 "window"    ;
stol             N1 "chair"     ;
ljud             N2 "sound"     ;
bord             N2 "table"     ;
barn             N2 "child"     ;
djur             N2 "animal"    ;
huvud            N2 "head"      ;
folk             N2 "people"    ;

LEXICON Adverbs

inte     Adv-lex ;
också    Adv-lex ;
när      Adv-lex ;
även     Adv-lex ;
då       Adv-lex ;
där      Adv-lex ;
nu       Adv-lex ;
aldrig   Adv-lex ;
alltid   Adv-lex ;
fast     Adv-lex ;
utomhus  Adv-lex ;
sedan    Adv-lex ;
i% går   Adv-lex ;
igen     Adv-lex ;
troligen Adv-lex ;
så       Adv-lex ;
sakta    Adv-lex ;
tillsammans  Adv-lex ;
naturligtvis Adv-lex ;
fortfarande  Adv-lex ;

LEXICON Adjectives

väldig         Adj-lex ;
tydlig         Adj-lex ;
kall           Adj-lex ;
varm           Adj-lex ;
vacker:vackE1r Adj-lex ;
glad:glaD1     Adj-lex ;
gammal:gammA1l Adj-lex ;
stor           Adj-lex ;
snabb          Adj-lex ;

LEXICON Conjunctions

och   Conj-lex ;
men   Conj-lex ;
att   Conj-lex ;
eller Conj-lex ;

LEXICON Determiners
 en:e  Det-en-lex ;
den:de Det-den-lex ;

LEXICON Prepositions
i       Prep-lex ;
på      Prep-lex ;
över    Prep-lex ;
under   Prep-lex ;
för     Prep-lex ;
ut      Prep-lex ;
till    Prep-lex ;
med     Prep-lex ;
framför Prep-lex ;
bakom   Prep-lex ;
om      Prep-lex ;
att     Prep-lex ;
genom   Prep-lex ;
efter   Prep-lex ;
från    Prep-lex ;
inuti   Prep-lex ;

LEXICON Propernouns
Mary  Proper-lex ;
James Proper-lex ;

LEXICON Pronouns
jag%<pron%>%<sg%>%<subj%>:jag  # ;
jag%<pron%>%<sg%>%<obj%>:mig   # ;
 du%<pron%>%<sg%>%<subj%>:du   # ;
 du%<pron%>%<sg%>%<obj%>:dig   # ;
hon%<pron%>%<sg%>%<subj%>:hon  # ;
hon%<pron%>%<sg%>%<obj%>:henne # ;
hon%<pron%>%<sg%>%<px%>:hennes # ;
han%<pron%>%<sg%>%<subj%>:han  # ;
han%<pron%>%<sg%>%<obj%>:honom # ;
han%<pron%>%<sg%>%<px%>:hans   # ;
sig%<pron%>%<refl%>%<obj%>:sig # ;
 vi%<pron%>%<pl%>%<subj%>:vi   # ;
 vi%<pron%>%<pl%>%<obj%>:oss   # ;
 ni%<pron%>%<pl%>%<subj%>:ni   # ;
 ni%<pron%>%<pl%>%<obj%>:er    # ;
 de%<pron%>%<pl%>%<subj%>:de   # ;
 de%<pron%>%<pl%>%<obj%>:dem   # ;
 de%<pron%>%<pl%>%<px%>:deras  # ;
 
 ingen%<pron%>:ing PRON_INGEN ;

LEXICON Numerals
två  Num-lex ;
tre  Num-lex ;
fyra Num-lex ;
fem  Num-lex ;
sex  Num-lex ;
sju  Num-lex ;
åtta Num-lex ;
nio  Num-lex ;
tio  Num-lex ;

LEXICON Verbs
! Irregular and strond verbs:
   vara:       V-vara-lex ;
   veta:v      V-veta-lex ;
  kunna:k      V-kunna-lex ;
 dricka:dr     V-dricka-lex ;
    äta:       V-äta-lex ;
     ha        V-ha-lex ;
     se:s      V-se-lex ;
     gå:g      V-gå-lex ;
     få:f      V-gå-lex ;
     ta:t      V-ta-lex ;

! Regular verbs:
  tänka:tänk   V-er-lex ;
skratta:skratt V-ar-lex ;
   leka:lek    V-ar-lex ;
  hitta:hitt   V-ar-lex ;
  svara:svar   V-ar-lex ;
berätta:berätt V-ar-lex ;
   leta:let    V-ar-lex ;
  titta:titt   V-ar-lex ;
   tala:tal    V-ar-lex ;
  fråga:fråg   V-ar-lex ;
  räkna:räkn   V-ar-lex ;
  prata:prat   V-ar-lex ;
  älska:älsk   V-ar-lex ;
  gilla:gill   V-ar-lex ;
  kolla:koll   V-ar-lex ;
 lyssna:lyssn  V-ar-lex ;
  sluta:slut   V-ar-lex ;

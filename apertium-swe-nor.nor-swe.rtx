! nor-swe.rtx

!! Attribute Categories: !!
gender = m f nt ut un fn @mf ;
number = (ND sg) sg pl sp ND ;
defnes = def ind ;
tense = inf pret pres pp supn imp ;
person = (PD p3) p1 p2 p3 PD ;
voice = (VD actv) pasv actv ;
a_adj = sint ord pp pprs ;
a_cmp = cmp ;
a_det = dem qnt pos emph ;
a_neg = neg ;
a_comp = pst comp sup ;

! Word sets:
w_paux = bli være vara vara¹ ;


!! Tag Rewrite Rules: !!
tense > tense : pret inf ;


!! Tag Order (Output Patterns): !!
adj:   _.a_adj.a_comp.gender.number.defnes.a_cmp;
n:     _.gender.number.defnes.a_cmp;
det:   _.a_det.gender.number;
vblex: _.tense.voice;
pr:    _;
adv:   _;
ij:    _;
cnjsub: _;
cnjcoo: _;
part:   _;

! Note: attrs not included here are not available to reduction rules
N:     _.gender.number.defnes.a_cmp;
A:     _.a_adj.a_comp.gender.number.defnes.a_cmp;
NP:    _.gender.number.defnes;
DP:    _.gender.number.defnes;
PP:    _;
VP:    _.tense.number.voice;
ADV:   _;
! TODO: Put gender/number on VP for pp-samsvar stuff?



!! Reduction Rules: !!

N -> %n         { %1   }
   | n.*.cmp %N { 1 %2 }
   ;

A -> "A->adj"       %adj {   %1 }
   | "A->n+A" n.*.cmp %A { 1 %2 }
   ;

NP -> %N { %1 }
    | A %N { 1 _ %2 }
    ;

PP -> pr %DP { 1 _ 2 } ;

DP -> det %NP
      { 1[gender=(if (2.number = pl) un else 2.gender), number=2.number] _ 2 }
    | "n.def det ~> det n.ind"
      %NP det.pos
      { 2[gender=(if (1.number = pl) un else 1.gender), number=1.number]
        _
        1[defnes=ind]
      }
      ;

! TODO: Do any tags need propagating here?
ADV -> "ADV" adv { 1 }
    | "ADV.nt" A.*.nt { 1 }
    ;

VP ->
    !   %vblex DP { 1[tense=$tense, person=$person, number=$number] _ 2 }
    ! | adv %VP (if (1.lem/sl = there)
    !               { %2 }
    !            else
    !               { 1 _ %2 } )
    ! | PP %VP { 1 _ %2 }
    ! |
    "er hardt rammet ~> drabbats hårt"
    %vblex ADV vblex.pp
    !! Would prefer to do %[w_paux]@vblex but
    !! https://github.com/apertium/apertium-recursive/issues/75
    ?(1.lemh/tl IsSubstring vara or 1.lemh/tl = bli )
    { 3[voice = pasv] _ 2 }
    |
    "rm att"
    %vblex.*.pasv part ha@vblex vblex.pp
    { 1 _ 3 _ 4 }
    ;
